alias physicalSP S0;
physicalSP=[PTBR+(2*SP/512)]*512+(SP%512);

alias sysCallNo S1;
sysCallNo=[physicalSP-1];

if(sysCallNo==9) then

	alias fileName S2;
	fileName=[physicalSP-3];

	alias i S3;

	i=0;
	while(i<=63) do
		if([FAT+8*i]==fileName) then
			break;
		endif;
		i=i+1;
	endwhile;

	if(i==64) then
		[physicalSP-2]=-1;
	ireturn;
	endif;

	load(1,[FAT+8*i+2]);

	alias count S4;
	alias cnt S5;

	count=0;
	i=0;
	while(i<=2) do
		if([SCRATCHPAD+i]==-1) then
			break;
		endif;
		count=count+1;
		i=i+1;
	endwhile;

	i=0;
	cnt=0;
	while(i<=2) do
		if([PTBR+2*i]==-1) then
			break;
		endif;
		i=i+1;
		cnt=cnt+1;
	endwhile;

	if(cnt>count) then
		i=0;
		while(count+i<cnt) do
			[MEM_LIST+[PTBR+2*(count+i)]]=0;
			[PTBR+2*(count+i)]=-1;
			[PTBR+2*(count+i)+1]="00";
			i=i+1;
		endwhile;
	endif;

	if(count>cnt) then
		alias free S15;
		alias diff S14;
		diff=count-cnt;
		i=0;
		free=0;
		while(i<=63) do
			if([MEM_LIST+i]==0) then
				free=free+1;
			endif;
			i=i+1;
		endwhile;
		if(free<diff) then
			[physicalSP-2]=-1;
			ireturn;
		endif;
		i=0;
		while(cnt+i<count) do
			free=0;
			while(free<=63) do
				if([MEM_LIST+free]==0) then
					break;
				endif;
				free=free+1;
			endwhile;
			[MEM_LIST+free]=1;
			[PTBR+2*(cnt+i)]=free;
			[PTBR+2*(cnt+i)+1]="01";
			i=i+1;
		endwhile;
	endif;

	i=0;
	while(i<count) do
		load([PTBR+2*i],[SCRATCHPAD+i]);
		i=i+1;
	endwhile;

	alias pid S6;
	pid=(PTBR-1024)/8;

	i=0;
	while(i<=7) do
		if([READY_LIST+32*pid+15+2*i]!=-1) then
			alias swoftIndex S13;
			swoftIndex=[READY_LIST+32*pid+15+2*i];
			[FILE_TABLE+2*swoftIndex+1]=[FILE_TABLE+2*swoftIndex+1]-1;
			if([FILE_TABLE+2*swoftIndex+1]==0) then
				[FILE_TABLE+2*swoftIndex]=-1;
				[FILE_TABLE+2*swoftIndex+1]=0;
			endif;
			[READY_LIST+32*pid+15+2*i]=-1;
			[READY_LIST+32*pid+15+2*i+1]=-1;
		endif;
		i=i+1;
	endwhile;

	SP=3*512;
	physicalSP=[PTBR+(2*SP/512)]*512+(SP%512);

	[physicalSP]=0;

	ireturn;

endif;

if(sysCallNo == 11) then
	alias currentPID S3;
	currentPID = (PTBR-1024)/8;
	if(currentPID<0 || currentPID>31) then
		[physicalSP-2]=-1;
		ireturn;
	endif;
	[physicalSP-2]=currentPID;
	ireturn;
endif;

if(sysCallNo == 12) then
	alias currentPID S3;
	alias parentPID S4;
	currentPID = (PTBR-1024)/8;
	parentPID = [READY_LIST + currentPID*32 + 31];
	if(currentPID<0 || currentPID>31) then
		[physicalSP-2]=-1;
		ireturn;
	endif;
	if(parentPID<0 || parentPID>31) then
		[physicalSP-2]=-1;
		ireturn;
	endif;	
	[physicalSP-2]=parentPID;
	ireturn;
endif;