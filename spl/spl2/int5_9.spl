alias physicalSP S0;
physicalSP=[PTBR+(2*SP/512)]*512+(SP%512);

alias sysCallNo S1;
sysCallNo=[physicalSP-1];

if(sysCallNo==8) then
alias i S2;
i=0;

while(i<=31) do
if([READY_LIST+32*i+1]==0) then
break;
endif;
i=i+1;
endwhile;

if(i==32) then
[physicalSP-2]=-1;
ireturn;
endif;

alias childPID S3;
childPID=i;

[READY_LIST+32*childPID]=i;

alias count S4;
count=0;
i=0;

while(i<=3) do
if([PTBR+2*i]!=-1) then
count=count+1;
endif;
i=i+1;
endwhile;

alias free S5;
free=0;
i=0;

while(i<=63) do
if([MEM_LIST+i]==0) then
free=free+1;
endif;
i=i+1;
endwhile;

if(free<count) then
[physicalSP-2]=-1;
ireturn;
endif;

alias childPTBR S6;
childPTBR=1024+8*childPID;


i=0;
while(i<=3) do
if([PTBR+2*i]!=-1) then
free=0;
while(free<=63) do
if([MEM_LIST+free]==0) then
break;
endif;
free=free+1;
endwhile;
[MEM_LIST+free]=1;
count=0;
while(count<=511) do
[free*512+count]=[[PTBR+2*i]*512+count];
count=count+1;
endwhile;
[childPTBR+2*i]=free;
[childPTBR+2*i+1]=[PTBR+2*i+1];
else
[childPTBR+2*i]=[PTBR+2*i];
[childPTBR+2*i+1]=[PTBR+2*i+1];
endif;
i=i+1;
endwhile;

[READY_LIST+32*childPID+5]=childPTBR;

[READY_LIST+32*childPID+4]=[physicalSP];

[READY_LIST+32*childPID+3]=SP-1;

[READY_LIST+32*childPID+2]=BP;

[READY_LIST+32*childPID+6]=PTLR;

[READY_LIST+32*childPID+7]=R0;

[READY_LIST+32*childPID+8]=R1;

[READY_LIST+32*childPID+9]=R2;

[READY_LIST+32*childPID+10]=R3;

[READY_LIST+32*childPID+11]=R4;

[READY_LIST+32*childPID+12]=R5;

[READY_LIST+32*childPID+13]=R6;

[READY_LIST+32*childPID+14]=R7;

alias parentPID S7;
parentPID=(PTBR-1024)/8;

i=0;
while(i<=7) do
[READY_LIST+32*childPID+15+2*i]=[READY_LIST+32*parentPID+15+2*i];
[READY_LIST+32*childPID+15+2*i+1]=[READY_LIST+32*parentPID+15+2*i+1];
if([READY_LIST+32*parentPID+15+2*i]!=-1) then
[FILE_TABLE+2*[READY_LIST+32*parentPID+15+2*i]+1]=[FILE_TABLE+2*[READY_LIST+32*parentPID+15+2*i]+1]+1;
endif;
i=i+1;
endwhile;

[READY_LIST+32*childPID+1]=1;

[physicalSP-2]=childPID;

alias childSP S8;
childSP=SP-1;

alias childPhySP S9;
childPhySP=[childPTBR+2*(childSP/512)]*512+(childSP%512);

[childPhySP-1]=-2;

ireturn;
endif;
